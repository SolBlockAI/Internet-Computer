load("@aspect_rules_sol//sol:defs.bzl", "sol_binary")
load("@io_bazel_rules_closure//closure:defs.bzl", "closure_js_library", "closure_js_test")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_doc", "rust_doc_test", "rust_library", "rust_test")
load("//bazel:canisters.bzl", "rust_canister")

package(default_visibility = ["//visibility:public"])

sol_binary(
    name = "minter_contract",
    srcs = ["minter.sol"],
)

# Export the compiled bytecode and ABI files as artifacts
filegroup(
    name = "contract_artifacts",
    srcs = [
        ":minter_contract",
    ],
    visibility = ["//visibility:public"],
)

rust_library(
    name = "minter",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = [
            "src/main.rs",
            "src/dashboard.rs",
            "src/dashboard/tests.rs",
        ],
    ),
    crate_name = "ic_cketh_minter",
    proc_macro_deps = [
        "@crate_index//:ic-cdk-macros",
        "@crate_index//:strum_macros",
    ],
    version = "0.1.0",
    deps = [
        "//packages/icrc-ledger-client-cdk:icrc_ledger_client_cdk",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/crypto/ecdsa_secp256k1",
        "//rs/crypto/sha3",
        "//rs/phantom_newtype",
        "//rs/types/ic00_types",
        "//rs/utils/ensure",
        "@crate_index//:candid",
        "@crate_index//:ethnum",
        "@crate_index//:futures",
        "@crate_index//:hex",
        "@crate_index//:hex-literal",
        "@crate_index//:ic-canister-log",
        "@crate_index//:ic-cdk",
        "@crate_index//:ic-cdk-timers",
        "@crate_index//:ic-metrics-encoder",
        "@crate_index//:ic-stable-structures",
        "@crate_index//:minicbor",
        "@crate_index//:num-bigint",
        "@crate_index//:num-traits",
        "@crate_index//:rlp",
        "@crate_index//:serde",
        "@crate_index//:serde_bytes",
        "@crate_index//:serde_json",
        "@crate_index//:strum",
        "@crate_index//:thiserror",
        "@crate_index//:thousands",
    ],
)

rust_doc(
    name = "doc",
    crate = ":minter",
)

rust_doc_test(
    name = "doc_test",
    crate = ":minter",
)

rust_test(
    name = "lib_tests",
    crate = ":minter",
    deps = [
        ":minter",
        "//rs/crypto/test_utils/reproducible_rng",
        "@crate_index//:assert_matches",
        "@crate_index//:ethers-core",
        "@crate_index//:maplit",
        "@crate_index//:proptest",
        "@crate_index//:rand",
    ],
)

rust_test(
    name = "unit_tests",
    srcs = ["src/dashboard/tests.rs"],
    crate = ":_wasm_cketh_minter",
    data = [":cketh_minter.did"],
    env = {
        "CARGO_MANIFEST_DIR": "rs/ethereum/cketh/minter",
    },
    deps = [
        ":minter",
        "@crate_index//:maplit",
        "@crate_index//:scraper",
    ],
)

[rust_canister(
    name = "cketh_minter" + target_suffix,
    srcs = [
        "src/dashboard.rs",
        "src/main.rs",
    ],
    compile_data = [
        "templates/dashboard.html",
        "templates/principal_to_bytes.js",
    ],
    crate_features = features,
    crate_name = "ic_cketh_minter_canister",
    proc_macro_deps = [
        "@crate_index//:ic-cdk-macros",
    ],
    service_file = "cketh_minter.did",
    deps = [
        ":minter",
        "//packages/icrc-ledger-client-cdk:icrc_ledger_client_cdk",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/crypto/ecdsa_secp256k1",
        "//rs/rust_canisters/http_types",
        "@crate_index//:askama",
        "@crate_index//:candid",
        "@crate_index//:ethabi",
        "@crate_index//:futures",
        "@crate_index//:hex",
        "@crate_index//:ic-canister-log",
        "@crate_index//:ic-cdk",
        "@crate_index//:ic-cdk-timers",
        "@crate_index//:ic-metrics-encoder",
        "@crate_index//:num-traits",
        "@crate_index//:serde_bytes",
        "@crate_index//:serde_json",
        "@crate_index//:time",
    ],
) for (target_suffix, features) in [
    ("", []),
    (
        "_debug",
        ["debug_checks"],
    ),
]]

rust_binary(
    name = "principal_to_hex",
    srcs = ["bin/principal_to_hex.rs"],
    deps = [
        "@crate_index//:candid",
        "@crate_index//:hex",
    ],
)

rust_test(
    name = "integration_tests",
    srcs = ["tests/tests.rs"],
    data = [
        ":cketh_minter_debug.wasm",
        "//rs/canister_sandbox",
        "//rs/canister_sandbox/sandbox_launcher",
        "//rs/rosetta-api/icrc1/ledger:ledger_canister_u256.wasm",
    ],
    env = {
        "CARGO_MANIFEST_DIR": "rs/ethereum/cketh/minter",
        "CKETH_MINTER_WASM_PATH": "$(rootpath :cketh_minter_debug.wasm)",
        "IC_ICRC1_LEDGER_WASM_PATH": "$(rootpath //rs/rosetta-api/icrc1/ledger:ledger_canister_u256.wasm)",
        "LAUNCHER_BINARY": "$(rootpath //rs/canister_sandbox/sandbox_launcher)",
        "SANDBOX_BINARY": "$(rootpath //rs/canister_sandbox)",
    },
    proc_macro_deps = [
        "@crate_index//:strum_macros",
    ],
    deps = [
        ":minter",
        "//packages/icrc-ledger-types:icrc_ledger_types",
        "//rs/rosetta-api/icrc1/ledger",
        "//rs/rust_canisters/http_types",
        "//rs/state_machine_tests",
        "//rs/test_utilities/load_wasm",
        "//rs/types/base_types",
        "@crate_index//:candid",
        "@crate_index//:ethers-core",
        "@crate_index//:hex",
        "@crate_index//:ic-cdk",
        "@crate_index//:num-traits",
        "@crate_index//:rlp",
        "@crate_index//:serde",
        "@crate_index//:serde_bytes",
        "@crate_index//:serde_json",
        "@crate_index//:strum",
    ],
)

closure_js_library(
    name = "principal_to_bytes",
    srcs = ["templates/principal_to_bytes.js"],
)

closure_js_test(
    name = "principal_to_bytes_test",
    srcs = ["tests/principal_to_bytes_test.js"],
    deps = [
        ":principal_to_bytes",
        "@com_google_javascript_closure_library//closure/goog/testing:asserts",
        "@com_google_javascript_closure_library//closure/goog/testing:jsunit",
    ],
)
